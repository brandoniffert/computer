" .vimrc
" Brandon Iffert <brandoniffert@gmail.com>

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" SETUP
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set nocompatible
filetype off
set rtp+=~/.vim/bundle/vundle/
call vundle#rc()

" github repos
Bundle 'gmarik/vundle'
Bundle 'tpope/vim-commentary'
Bundle 'tpope/vim-rails'
Bundle 'tpope/vim-surround'
Bundle 'Lokaltog/vim-powerline'
Bundle 'kien/ctrlp.vim'
Bundle 'altercation/vim-colors-solarized'
Bundle 'ervandew/supertab'
Bundle 'tmhedberg/matchit'
Bundle 'wincent/Command-T'
Bundle 'w0ng/vim-hybrid'
Bundle 'Lokaltog/vim-easymotion'

filetype plugin indent on

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" GENERAL
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set encoding=utf-8
set nomodeline
set showtabline=2
set hidden
set ruler
set number
set backspace=eol,start,indent
set laststatus=2
set history=10000
set showbreak=↪
set autoread
set cursorline
set cmdheight=2
set scrolloff=3
set fillchars=vert:¦

" Powerline shows the mode
set noshowmode

" Tabs & Indenting
set expandtab tabstop=2 shiftwidth=2 softtabstop=2
set shiftround
set autoindent smarttab smartindent

" Backups
set backup 
set backupdir=~/.vim-tmp,~/.tmp,~/tmp,/var/tmp,/tmp 
set backupskip=/tmp/*,/private/tmp/* 
set directory=~/.vim-tmp,~/.tmp,~/tmp,/var/tmp,/tmp 
set writebackup

" Wildmenu
set wildmenu
set wildmode=longest,list:longest,list:full
set wildignore+=.git,.svn,*.jpg,*.jpeg,*.png,*.gif,*.sw?,*.DS_Store

" Searching
set showmatch
set incsearch
set hlsearch
set ignorecase smartcase

" Accordion horizontal splits
set winwidth=84
set winheight=10
set winminheight=10
set winheight=999

" Folding
set foldmethod=marker
set foldlevelstart=0
nnoremap <space> za

let mapleader=","

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" ENVIRONMENTS AND COLOR
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
syntax enable
set t_Co=256

set bg=dark
colorscheme Tomorrow-Night
hi Normal ctermbg=NONE
hi LineNr ctermfg=239

" Read ITERM_PROFILE enviornment variable and set colorscheme accordingly
let iterm_profile = $ITERM_PROFILE
if iterm_profile == "solarized-light"
  set bg=light
  colorscheme solarized
  hi VertSplit ctermbg=NONE
  set showtabline=0
endif
if iterm_profile == "solarized-dark"
  set bg=dark
  colorscheme solarized
  set showtabline=0
endif

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" KEY MAPS
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
inoremap jj <Esc>
nnoremap j gj
nnoremap k gk
nnoremap <CR> :nohlsearch<cr>
" use hjkl for switching between splits
nnoremap <C-j> <C-W>j
nnoremap <C-h> <C-W>h
nnoremap <C-k> <C-W>k
nnoremap <C-l> <C-W>l

let g:EasyMotion_leader_key = '<Leader>m'

" use Command-T if available...
if has('ruby')
  let g:CommandTCancelMap=['<esc>', '<C-c>']
  let g:CommandTMaxHeight=25
  nnoremap <silent> <leader>f :CommandTFlush<cr>:CommandT<cr>
else
" else CtrlP
  let cwd = getcwd()
  nnoremap <leader>f :CtrlP cwd<cr><f5><cr>
endif

" select text that was just pasted
nnoremap <leader>v V`]
" create a new vertical split window and switch over to it
nnoremap <leader>sv :botright vnew<cr>
" create a new horizontal split window
nnoremap <leader>sh :sp<cr>
" jump back and forth between files
nnoremap <leader><leader> <C-^>
" open .vimrc to edit in the current window
nnoremap <leader>ev :e ~/.vimrc<cr>
" open .vimrc to edit in a vertical split
nnoremap <leader>esv :vsp ~/.vimrc<cr>
" change working directory to file being edited, print after
nnoremap <leader>cd :cd %:p:h<CR>:pwd<CR>
" easy indent/outdent
nnoremap <Tab> >>
nnoremap <S-Tab> <<
vnoremap <Tab> >gv 
vnoremap <S-Tab> <gv
" yank to system clipboard
nnoremap <leader>y "*y
" paste from system clipboard
nnoremap <leader>p "*p
" toggle spell check
nnoremap <leader>ss :setlocal spell!<cr>

" open directory of file in Finder
if has('mac')
  nnoremap <leader>o :silent !open %:p:h<cr> 
endif

nmap Y y$
map Q gq

function! RunCurrentFile()
  let ft = &ft
  exec ':w'

  if ft == 'ruby'
    exec ':!ruby %'
  endif
  if ft == 'php'
    exec ':!php -f %'
  endif
  if ft == 'sh'
    exec ':!bash %'
  endif
endfunction
nnoremap <leader>r :call RunCurrentFile()<cr>

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" AUTOCOMMANDS
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
if has("autocmd")
  " Put these in an autocmd group, so that we can delete them easily.
  augroup vimrcEx
    au!
    " For all text files set 'textwidth' to 78 characters.
    au FileType text setlocal textwidth=78

    au BufNewFile,BufRead *.ss,*.erb silent set ft=html
    au BufNewFile,BufRead /etc/nginx/*.conf silent set ft=nginx
    au BufNewFile,BufRead * setlocal formatoptions=cqnl

    au FileType python set sta=4 sw=4 sts=4 et

    " When editing a file, always jump to the last known cursor position.
    " Don't do it when the position is invalid or when inside an event handler
    " (happens when dropping a file on gvim).
    au BufReadPost *
      \ if line("'\"") > 0 && line("'\"") <= line("$") |
      \   exe "normal g`\"" |
      \ endif
  augroup END
endif

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" RENAME CURRENT FILE
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
function! RenameFile()
  let old_name = expand('%')
  let new_name = input('New file name: ', expand('%'), 'file')
  if new_name != '' && new_name != old_name
    exec ':saveas ' . new_name
    exec ':silent !rm ' . old_name
    redraw!
  endif
endfunction
map <leader>n :call RenameFile()<cr>

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" SHOW SYNTAX HIGHLIGHTING GROUPS FOR WORD UNDER CURSOR
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
nmap <leader>hi :call <SID>SynStack()<CR>
function! <SID>SynStack()
  if !exists("*synstack")
    return
  endif
  echo map(synstack(line('.'), col('.')), 'synIDattr(v:val, "name")')
endfunc

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" USE LOCAL .vimrc.local
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
if filereadable(expand("$HOME/.vimrc.local"))
  so $HOME/.vimrc.local
endif
